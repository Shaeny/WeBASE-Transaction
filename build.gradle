version '1.0'

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// In this section you declare where to find the dependencies of your project
repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/"}
    mavenCentral()
    maven { url 'https://dl.bintray.com/ethereum/maven/'}
    mavenLocal()
}

def springboot_version="2.1.16.RELEASE"
List springboot = [
    "org.springframework.boot:spring-boot-starter-web:$springboot_version",
    "org.springframework.boot:spring-boot-autoconfigure:$springboot_version",
    "org.springframework.boot:spring-boot-configuration-processor:$springboot_version",
//    "org.springframework.boot:spring-boot-starter-log4j2:$springboot_version"
]

// cover old version
def spring_version="5.2.17.RELEASE"
List spring = [
    "org.springframework:spring-jcl:$spring_version",
    "org.springframework:spring-tx:$spring_version"
]
List swagger = [
    'io.springfox:springfox-swagger2:2.8.0',
    'io.springfox:springfox-swagger-ui:2.8.0'
]

def log4j_version="2.15.0"
List logger = [
    "org.apache.logging.log4j:log4j-api:$log4j_version",
    "org.apache.logging.log4j:log4j-core:$log4j_version",
    "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version",
    "org.apache.logging.log4j:log4j-web:$log4j_version",
    "org.slf4j:jcl-over-slf4j:1.7.12"
]

List jaxb = [
     "javax.xml.bind:jaxb-api:2.3.0",
     "com.sun.xml.bind:jaxb-impl:2.3.0",
     "com.sun.xml.bind:jaxb-core:2.3.0",
     "javax.activation:activation:1.1.1"
]

List lombok = [
    "org.projectlombok:lombok:1.18.2"
]

def jackson_version="2.11.0"
List jackson = [
    "com.fasterxml.jackson.core:jackson-databind:$jackson_version",
    "com.fasterxml.jackson.module:jackson-module-parameter-names:$jackson_version",
    "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_version",
    "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
]

List mysql = [
    'mysql:mysql-connector-java:8.0.26',
    'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.0.0'
]

dependencies {
    compile springboot,swagger,logger,jaxb,jackson,mysql,spring
    
    compile "org.apache.commons:commons-lang3:3.8.1"
    compile "org.fisco-bcos:web3sdk:2.5.1"
    compile "org.bouncycastle:bcprov-jdk15on:1.67"
    // cover old version
    compile 'org.yaml:snakeyaml:1.29'
    compile 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.54'

    compile lombok
    annotationProcessor lombok
}

configurations {
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'com.mchange', module: '*'
    all*.exclude group: 'org.ow2.asm', module: 'asm'
    all*.exclude group: 'jline', module: 'jline'
    all*.exclude group: 'com.google.protobuf', module: 'protobuf-java'
    all*.exclude group: 'javax.annotation', module: 'javax.annotation-api'
    all*.exclude group: 'org.ethereum'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources  {
            srcDir 'src/main/resources'
        }
    }
}

clean {
    delete 'dist'
    delete 'build'
    delete 'log'
}

jar {
    destinationDir file('dist/apps')
    archiveName project.name + '.jar'
    exclude '**/*.xml'
    exclude '**/*.properties'

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf_template'
        }
        copy {
            from configurations.runtime
            into 'dist/lib'
        }
        copy {
            from file('.').listFiles().findAll{File f -> (f.name.endsWith('.sh') || f.name.endsWith('.env'))}
            into 'dist'
        }
    }
}
